## Natural Language Toolkit: feat0.fcfg
##
## First example of a feature-based grammar for English, illustrating
## value-sharing of NUM and TENSE features.
## Used in Feature-Based Grammars chapter.
## 
## Author: Ewan Klein <ewan@inf.ed.ac.uk> 
## URL: <http://nltk.sourceforge.net>
## For license information, see LICENSE.TXT

% start S
# ###################
# Grammar Productions
# ###################

# S expansion productions
S -> NP[NUM=?n] VP[NUM=?n]

# NP expansion productions
NP[NUM=?n] -> N[NUM=?n] 
NP[NUM=?n] -> PropN[NUM=?n] 
NP[NUM=?n] -> Det[NUM=?n] N[NUM=?n]
NP[NUM=pl] -> N[NUM=pl] 

# VP expansion productions
VP[TENSE=?t, NUM=?n] -> IV[TENSE=?t, NUM=?n]
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n] NP

# added
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n] NP PP
VP[TENSE=?t, NUM=?n] -> IV[TENSE=?t, NUM=?n] PP

PP -> P NP

# ###################
# Lexical Productions
# ###################

Det[NUM=sg] -> 'this' | 'every' | 'a'
Det[NUM=pl] -> 'these' | 'all'
Det -> 'the' | 'some' | 'several' | 'an' | 'my'

PropN[NUM=sg]-> 'John' | 'Mary' | 'Bob' | 'Pookie' | 'Pete' | 'Jane' | 'Sam'

N[NUM=sg] -> 'cat' | 'man' | 'dog' | 'telescope' | 'park' | 'duck' | 'bus'
N[NUM=pl] -> 'cats' | 'men' | 'dogs' | 'telescopes' | 'parks' | 'ducks' | 'buses' 

IV[TENSE=pres,  NUM=sg] -> 'disappears' | 'walks' | 'runs' | 'flies'
TV[TENSE=pres, NUM=sg] -> 'sees'

IV[TENSE=pres,  NUM=pl] -> 'disappear' | 'walk' | 'run' | 'fly'
TV[TENSE=pres, NUM=pl] -> 'see'

IV[TENSE=past] -> 'disappeared' | 'walked' | 'ran' | 'flew'
TV[TENSE=past] -> 'saw'

P -> 'in' | 'on' | 'by' | 'with' | 'at'
